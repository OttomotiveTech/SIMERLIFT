FUNCTION_BLOCK  essBattState  {
    width := 400;
    bgColor := "mediumseagreen";
  }

VAR_INPUT
	IGNITION : BOOL;
	chargerON:BOOL;
	prechargeOk:BOOL;
	dischargeComplete:BOOL;
	chargeComplete:BOOL;
	shutdownComplete:BOOL;
	contactFbk:BOOL;
	currentAmp:REAL;
	outVoltStatus:BOOL;
	OVfault:BOOL;
    UVfault:BOOL;
	fault:BOOL;
END_VAR

VAR_OUTPUT
	STATE : BATTSTATE;
	prechargeOn:BOOL;
	contact:BOOL;
	contactNeg:BOOL;
END_VAR


VAR
currentCharging:BOOL;
currentDischarging:BOOL;

CURRENTTIMERcharging:TON;
CURRENTTIMERdischarging:TON;

chargeVoltTimer:Tbuffer;
dischargeVoltTimer:Tbuffer;

BATTERYSTATE:BATTSTATE:=BATTSTATE#STANDBY;
PREVSTATE:BATTSTATE:=BATTSTATE#STANDBY;

BATTIMER:TON;
BATSTATEINTERVAL:TIME:=T#10s;
BATSTATETIMEOUT:BOOL;
BATSTATEELAPSEDTIME:TIME;
STANDBYINTERVAL:TIME:=T#2s;
END_VAR


CURRENTTIMERcharging(IN:=currentAmp<-0.5, PT:=T#500ms, Q=>currentCharging);
CURRENTTIMERdischarging(IN:=currentAmp>0.5, PT:=T#500ms, Q=>currentDischarging);
BATTIMER(IN:=BATTERYSTATE=PREVSTATE, PT:=BATSTATEINTERVAL, Q=>BATSTATETIMEOUT, ET=>BATSTATEELAPSEDTIME);

PREVSTATE:=BATTERYSTATE;


CASE BATTERYSTATE OF
	
	BATTSTATE#STANDBY:  // standby
		STATE:=BATTSTATE#STANDBY;
		BATSTATEINTERVAL:=T#12s;
		
		prechargeOn:=0;
		contact:=0;
		contactNeg:=0;
		
		// at the first time wait 2 sec
		IF BATSTATEELAPSEDTIME>STANDBYINTERVAL THEN
		    // then wait 100ms in next sessions
			STANDBYINTERVAL:=T#500ms;
			IF fault THEN
				BATTERYSTATE:=BATTSTATE#FAULT;
			ELSIF IGNITION THEN
				IF chargerON THEN
					IF OVfault OR chargeComplete THEN
						BATTERYSTATE:=BATTSTATE#CHARGECOMPLETE;
					ELSE
						BATTERYSTATE:=BATTSTATE#PRECHARGE;
					END_IF;
				ELSE
					BATTERYSTATE:=BATTSTATE#PRECHARGE;
				END_IF;
			END_IF;	
				
		END_IF;
		
		
	BATTSTATE#PRECHARGE:  //discharge
		STATE:=BATTSTATE#PRECHARGE;
		BATSTATEINTERVAL:=T#5s;
		
		contactNeg:=1;
		
		IF BATSTATEELAPSEDTIME>T#50ms THEN
			prechargeOn:=1;
		ELSE
			prechargeOn:=0;
		END_IF;
		
		IF prechargeOk THEN
			contact:=1;
		ELSE
			contact:=0;
		END_IF;
		
		IF fault OR  NOT IGNITION  THEN
			BATTERYSTATE:=BATTSTATE#SHUTDOWN;
		ELSIF BATSTATETIMEOUT THEN
			BATTERYSTATE:=BATTSTATE#FAULT;
		ELSIF prechargeOk=TRUE AND contactFbk=TRUE THEN
			IF chargerON THEN
				IF OVfault OR chargeComplete THEN
					BATTERYSTATE:=BATTSTATE#CHARGECOMPLETE;
				ELSE
					BATTERYSTATE:=BATTSTATE#CHARGE;
				END_IF;
			ELSE
				BATTERYSTATE:=BATTSTATE#DISCHARGE;
			END_IF;
		END_IF;
		
		
	BATTSTATE#DISCHARGE:  //discharge
		STATE:=BATTSTATE#DISCHARGE;
		BATSTATEINTERVAL:=T#120s;
		
		prechargeOn:=0;
		contact:=1;
		contactNeg:=1;

		IF fault THEN
			BATTERYSTATE:=BATTSTATE#SHUTDOWN;
		ELSIF NOT IGNITION THEN
			BATTERYSTATE:=BATTSTATE#SHUTDOWN;
		ELSIF chargerON THEN
			IF OVfault OR chargeComplete THEN
				BATTERYSTATE:=BATTSTATE#CHARGECOMPLETE;
			ELSE
				BATTERYSTATE:=BATTSTATE#CHARGE;
			END_IF;
		ELSIF BATSTATEELAPSEDTIME>T#10s THEN   
			IF currentDischarging THEN
				IF UVfault OR dischargeComplete THEN
					BATTERYSTATE:=BATTSTATE#DISCHARGECOMPLETE;
				END_IF;
			ELSIF currentCharging THEN
				IF OVfault THEN
					BATTERYSTATE:=BATTSTATE#CHARGECOMPLETE;
				END_IF;
			END_IF;
		ELSIF (UVfault OR dischargeComplete) AND currentAmp>10 THEN
			BATTERYSTATE:=BATTSTATE#DISCHARGECOMPLETE;
		ELSIF (OVfault OR chargeComplete) AND currentAmp<-10 THEN
			BATTERYSTATE:=BATTSTATE#CHARGECOMPLETE;
		END_IF;
		
		
	BATTSTATE#CHARGE:  // charge
		STATE:=BATTSTATE#CHARGE;
		BATSTATEINTERVAL:=T#120s;
		
		prechargeOn:=0;
		contact:=1;
		contactNeg:=1;

		IF fault THEN
			BATTERYSTATE:=BATTSTATE#SHUTDOWN;
		ELSIF NOT IGNITION THEN
			BATTERYSTATE:=BATTSTATE#SHUTDOWN;
		ELSIF NOT chargerON THEN
			IF UVfault THEN
				BATTERYSTATE:=BATTSTATE#DISCHARGECOMPLETE;
			ELSIF dischargeComplete THEN
				BATTERYSTATE:=BATTSTATE#DISCHARGECOMPLETE;
			ELSE
				BATTERYSTATE:=BATTSTATE#DISCHARGE;
			END_IF;
		ELSIF OVfault OR chargeComplete THEN   
			BATTERYSTATE:=BATTSTATE#CHARGECOMPLETE;
		ELSIF  BATSTATEELAPSEDTIME>T#8s THEN
			IF currentDischarging THEN
				IF UVfault THEN
					BATTERYSTATE:=BATTSTATE#DISCHARGECOMPLETE;
				ELSIF dischargeComplete THEN
					BATTERYSTATE:=BATTSTATE#DISCHARGECOMPLETE;
				END_IF;
			END_IF;
		ELSIF (UVfault OR dischargeComplete) AND currentAmp>10THEN
			BATTERYSTATE:=BATTSTATE#DISCHARGECOMPLETE;
		END_IF;
		
		
	BATTSTATE#SHUTDOWN:  // shutdown
		STATE:=BATTSTATE#SHUTDOWN;
		BATSTATEINTERVAL:=T#3000ms; 
		prechargeOn:=0;
		IF BATSTATETIMEOUT OR  (shutdownComplete AND (BATSTATEELAPSEDTIME>T#1000ms))  THEN
			contact:=0;
		 	contactNeg:=0;
		 	IF contactFbk=FALSE THEN
				IF fault THEN
					BATTERYSTATE:=BATTSTATE#FAULT;
				ELSIF UVfault THEN
					BATTERYSTATE:=BATTSTATE#DISCHARGECOMPLETE;
				ELSIF OVfault THEN
					BATTERYSTATE:=BATTSTATE#CHARGECOMPLETE;
				ELSIF NOT IGNITION THEN
					BATTERYSTATE:=BATTSTATE#STANDBY;
				END_IF;
			ELSIF BATSTATEELAPSEDTIME>T#2500ms THEN
				BATTERYSTATE:=BATTSTATE#FAULT;
			END_IF;

		END_IF;
		
		
	BATTSTATE#FAULT:  //fault
		STATE:=BATTSTATE#FAULT;
		BATSTATEINTERVAL:=T#10s;
		
		prechargeOn:=0;
		contact:=0;
		contactNeg:=0;
		
		IF NOT fault AND (NOT IGNITION OR BATSTATETIMEOUT) THEN
			BATTERYSTATE:=BATTSTATE#STANDBY;
		END_IF;

			
	BATTSTATE#CHARGECOMPLETE:  //charge complete
		STATE:=BATTSTATE#CHARGECOMPLETE;
		BATSTATEINTERVAL:=T#5s;
		
		prechargeOn:=0;
		contact:=0;
		contactNeg:=0;
		
		IF fault THEN
			BATTERYSTATE:=BATTSTATE#SHUTDOWN;
		ELSIF NOT IGNITION THEN
			BATTERYSTATE:=BATTSTATE#SHUTDOWN;
		ELSIF NOT outVoltStatus AND BATSTATETIMEOUT THEN
			IF UVfault THEN
				BATTERYSTATE:=BATTSTATE#DISCHARGECOMPLETE;
			ELSIF dischargeComplete THEN
				BATTERYSTATE:=BATTSTATE#DISCHARGECOMPLETE;
			ELSE
				BATTERYSTATE:=BATTSTATE#PRECHARGE;
			END_IF;
		END_IF;
		
		
	BATTSTATE#DISCHARGECOMPLETE:  //charge complete
		STATE:=BATTSTATE#DISCHARGECOMPLETE;
		BATSTATEINTERVAL:=T#5s;
		
		prechargeOn:=0;
		contact:=0;
		contactNeg:=0;
		
		IF fault THEN
			BATTERYSTATE:=BATTSTATE#SHUTDOWN;
		ELSIF NOT IGNITION THEN
			BATTERYSTATE:=BATTSTATE#SHUTDOWN;
		ELSIF outVoltStatus AND BATSTATETIMEOUT THEN
			IF OVfault THEN
				BATTERYSTATE:=BATTSTATE#CHARGECOMPLETE;
			ELSIF chargeComplete THEN
				BATTERYSTATE:=BATTSTATE#CHARGECOMPLETE;
			ELSE
				BATTERYSTATE:=BATTSTATE#PRECHARGE;   /// because no current will flow unless contact closed
			END_IF;
		END_IF;

			
END_CASE;

END_FUNCTION_BLOCK



